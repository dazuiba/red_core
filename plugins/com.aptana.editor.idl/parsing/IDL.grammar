// java -jar beaver.jar -T -w IDL.grammar
%package "com.aptana.editor.idl.parsing";

%class "IDLParser";

%import "com.aptana.parsing.IParseState";
%import "com.aptana.parsing.IParser";
%import "com.aptana.parsing.ast.IParseNode";

%implements "IParser";

%embed {:
	/*
	 * (non-Javadoc)
	 * @see com.aptana.parsing.IParser#parse(com.aptana.parsing.IParseState)
	 */
	@Override
	public synchronized IParseNode parse(IParseState parseState) throws java.lang.Exception
	{
		return null;
	}
:};

%terminals ANY, ATTRIBUTE, BOOLEAN, CONST, DOMSTRING, EXCEPTION, FLOAT, GETRAISES, IN, INTERFACE, LONG;
%terminals MODULE, OBJECT, OCTET, RAISES, READONLY, SEQUENCE, SETRAISES, SHORT, TYPEDEF, UNSIGNED, TRUE;
%terminals FALSE, VOID, NUM_INTEGER, NUM_FLOAT, LBRACKET, RBRACKET, LPAREN, RPAREN, LCURLY, RCURLY, SEMICOLON, COMMA;
%terminals COLON, DOUBLE_COLON, LESS_THAN, GREATER_THAN, EQUAL, IDENTIFIER, STRING, OTHER;
%terminals OMITTABLE, DOUBLE, QUESTION, ELLIPSIS, OPTIONAL, GETTER, SETTER, CREATOR, DELETER, CALLER, STRINGIFIER, IMPLEMENTS;

%goal Definitions;

// Level 0

Definitions
	=	ExtendedAttributeList Definition Definitions
	|	// empty
	;

Definition
	=	Module
	|	Interface
	|	Exception
	|	Typedef
	|	ImplementsStatement
	;

Module
	=	MODULE IDENTIFIER LCURLY Definitions RCURLY SEMICOLON
	;

Interface
	=	INTERFACE IDENTIFIER InterfaceInheritance LCURLY InterfaceMembers RCURLY SEMICOLON
	;

InterfaceInheritance
	=	COLON ScopedNameList
	|	// empty
	;

InterfaceMembers
	=	ExtendedAttributeList InterfaceMember InterfaceMembers
	|	// empty
	;

InterfaceMember
	=	Const
	|	AttributeOrOperation
	;

Exception
	=	EXCEPTION IDENTIFIER LCURLY ExceptionMembers RCURLY SEMICOLON
	;

ExceptionMembers
	=	ExtendedAttributeList ExceptionMember ExceptionMembers
	|	// empty
	;

Typedef
	=	TYPEDEF Type IDENTIFIER SEMICOLON
	;

ImplementsStatement
	=	ScopedName IMPLEMENTS ScopedName SEMICOLON
	;

Const
	=	CONST Type IDENTIFIER EQUAL ConstExpr SEMICOLON
	;

ConstExpr
	=	BooleanLiteral
	|	NUM_INTEGER
	|	NUM_FLOAT
	;

BooleanLiteral
	=	TRUE
	|	FALSE
	;

AttributeOrOperation
	=	STRINGIFIER StringifierAttributeOrOperation
	|	Attribute
	|	Operation
	;

StringifierAttributeOrOperation
	=	Attribute
	|	OperationRest
	|	SEMICOLON
	;

Attribute
	=	ReadOnly ATTRIBUTE Type IDENTIFIER GetRaises SetRaises SEMICOLON
	;

ReadOnly
	=	READONLY
	|	// empty
	;

GetRaises
	=	GETRAISES ExceptionList
	|	// empty
	;

SetRaises
	=	SETRAISES ExceptionList
	|	// empty
	;

Operation
	=	OmittableSpecials OperationRest
	;

OmittableSpecials
	=	OMITTABLE Specials
	|	Specials
	;

Specials
	=	Special Specials
	|	// empty
	;

Special
	=	GETTER
	|	SETTER
	|	CREATOR
	|	DELETER
	|	CALLER
	;

OperationRest
	=	ReturnType OptionalIdentifier LPAREN ArgumentList RPAREN Raises SEMICOLON
	;

OptionalIdentifier
	=	IDENTIFIER
	|	// empty
	;

Raises
	=	RAISES ExceptionList
	|	// empty
	;

ExceptionList
	=	LPAREN ScopedNameList RPAREN
	;

ArgumentList
	=	Argument Arguments
	|	// empty
	;

Arguments
	=	COMMA Argument Arguments
	|	// empty
	;

Argument
	=	ExtendedAttributeList In Optional Type Ellipsis IDENTIFIER
	;

In
	=	IN
	|	// empty
	;

Optional
	=	OPTIONAL
	|	// empty
	;

Ellipsis
	=	ELLIPSIS
	|	// empty
	;

ExceptionMember
	=	Const
	|	ExceptionField
	;

ExceptionField
	=	Type IDENTIFIER SEMICOLON
	;

ExtendedAttributeList
	=	LBRACKET ExtendedAttribute ExtendedAttributes RBRACKET
	|	// empty
	;

ExtendedAttributes
	=	COMMA ExtendedAttribute ExtendedAttributes
	|	// empty
	;

ExtendedAttribute
	=	LPAREN ExtendedAttributeInner RPAREN ExtendedAttributeRest
	|	LBRACKET ExtendedAttributeInner RBRACKET ExtendedAttributeRest
	|	LCURLY ExtendedAttributeInner RCURLY ExtendedAttributeRest
	|	Other ExtendedAttributeRest
	;

ExtendedAttributeRest
	=	ExtendedAttribute
	|	// empty
	;

ExtendedAttributeInner
	=	LPAREN ExtendedAttributeInner RPAREN ExtendedAttributeInner
	|	LBRACKET ExtendedAttributeInner RBRACKET ExtendedAttributeInner
	|	LCURLY ExtendedAttributeInner RCURLY ExtendedAttributeInner
	|	OtherOrComma ExtendedAttributeInner
	|	// empty
	;

Other
	=	NUM_INTEGER
	|	NUM_FLOAT
	|	IDENTIFIER
	|	STRING
	|	OTHER
	|	ELLIPSIS
	|	COLON
	|	DOUBLE_COLON
	|	SEMICOLON
	|	LESS_THAN
	|	EQUAL
	|	GREATER_THAN
	|	QUESTION
	|	FALSE
	|	OBJECT
	|	TRUE
	|	ANY
	|	ATTRIBUTE
	|	BOOLEAN
	|	CALLER
	|	CONST
	|	CREATOR
	|	DELETER
	|	DOUBLE
	|	EXCEPTION
	|	FLOAT
	|	GETRAISES
	|	GETTER
	|	IMPLEMENTS
	|	IN
	|	INTERFACE
	|	LONG
	|	MODULE
	|	OCTET
	|	OMITTABLE
	|	OPTIONAL
	|	RAISES
	|	SEQUENCE
	|	SETRAISES
	|	SETTER
	|	SHORT
	|	DOMSTRING
	|	STRINGIFIER
	|	TYPEDEF
	|	UNSIGNED
	|	VOID
	;

OtherOrComma
	=	Other
	|	COMMA
	;

Type
	=	NullableType
	|	ScopedName
	|	ANY
	|	OBJECT
	;

NullableType
	=	UnsignedIntegerType Nullable
	|	BOOLEAN Nullable
	|	OCTET Nullable
	|	FLOAT Nullable
	|	DOUBLE Nullable
	|	DOMSTRING Nullable
	|	SEQUENCE LESS_THAN Type GREATER_THAN Nullable
	;

UnsignedIntegerType
	=	UNSIGNED IntegerType
	|	IntegerType
	;

IntegerType
	=	SHORT
	|	LONG OptionalLong
	;

OptionalLong
	=	LONG
	|	// empty
	;

Nullable
	=	QUESTION
	|	// empty
	;

ReturnType
	=	Type
	|	VOID
	;

ScopedNameList
	=	ScopedName ScopedNames
	;

ScopedNames
	=	COMMA ScopedName ScopedNames
	|	// empty
	;

ScopedName
	=	AbsoluteScopedName
	|	RelativeScopedName
	;

AbsoluteScopedName
	=	DOUBLE_COLON IDENTIFIER ScopedNameParts
	;

RelativeScopedName
	=	IDENTIFIER ScopedNameParts
	;

ScopedNameParts
	=	DOUBLE_COLON IDENTIFIER ScopedNameParts
	|	// empty
	;

/*

ExtendedAttributeNoArg
	=	IDENTIFIER
	;

ExtendedAttributeArgList
	=	IDENTIFIER LPAREN ArgumentList RPAREN
	;

ExtendedAttributeIdent
	=	IDENTIFIER EQUAL IDENTIFIER
	;

ExtendedAttributeScopedName
	=	IDENTIFIER EQUAL ScopedName
	;

ExtendedAttributeNamedArgList
	=	IDENTIFIER EQUAL IDENTIFIER LPAREN ArgumentList RPAREN
	;

*/

